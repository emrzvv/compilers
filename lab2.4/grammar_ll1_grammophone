
Program -> Entities .
Entities -> Function Entities | Procedure Entities | .

Function -> FunctionHeader Body "endfunc" .
Procedure -> ProcedureHeader Body "endproc" .
Body -> Statements .
FunctionHeader -> "func" "VARNAME" "(" Sections ")" "->" Type .
ProcedureHeader -> "proc" "VARNAME" "(" Sections ")" .
Sections -> Section SectionsTail .
SectionsTail -> ";" Section SectionsTail | .
Section -> "VARNAME" SectionTail "->" Type .
SectionTail -> "," "VARNAME" SectionTail | .
Type -> PrimitiveType | "[" Type "]" .
PrimitiveType -> "int" | "char" | "bool" .
Statements -> Statement StatementsTail .
StatementsTail -> ";" Statement StatementsTail | .
Statement -> DeclarationStatement | AssignmentStatement | ArrayAssignmentStatement | FunctionCallStatement | IfStatement | LoopWithPreconditionStatements | LoopWithPostconditionStatement | "check" Expression .

DeclarationStatement -> VarDeclarations "->" Type .
VarDeclarations -> VarDeclaration VarDeclarationsTail .
VarDeclarationsTail -> "," VarDeclaration VarDeclarationsTail | .
VarDeclaration -> "VARNAME" VarDeclarationTail .
VarDeclarationTail -> "=" Expression | .

AssignmentStatement -> "VARNAME" "=" Expression .
ArrayAssignmentStatement -> ArrayCall "=" Expression .
FunctionCallStatement -> "VARNAME" "(" Args ")" .
Args -> Expression ArgsTail | .
ArgsTail -> "," Expression ArgsTail | .
Var -> "VARNAME" .
ArrayCall -> Spec "[" Expression "]" .
Spec -> FunctionCallStatement | ArraySpec | Var | Const .
ArraySpec -> "[" Type ArraySpecSuffx .
ArraySpecSuffix -> "VARNAME" "]" | "INT_CONST" "]" .

Const -> "INT_CONST" | "CHAR_CONST" | "STRING_CONST" | "REF_CONST" | "TRUE" | "FALSE" .
Expression -> LogicalExpression ExpressionTail .
ExpressionTail -> "||" LogicalExpression ExpressionTail | "^^" LogicalExpression ExpressionTail | .
LogicalExpression -> CompareExpression LogicalExpressionTail .
LogicalExpressionTail -> "&&" CompareExpression LogicalExpressionTail | .
CompareExpression -> ArithmeticExpression CompareExpressionTail .
CompareExpressionTail -> CompareOperator ArithmeticExpression CompareExpressionTail | .
CompareOperator -> "==" | "!=" | "<" | ">" | "<=" | ">=" .
ArithmeticExpression -> MultiplicativeExpression ArithmeticExpressionTail .
ArithmeticExpressionTail -> AdditiveOperator MultiplicativeExpression ArithmeticExpressionTail | .
AdditiveOperator -> "+" | "-" .
MultiplicativeExpression -> Term MultiplicativeExpressionTail .
MultiplicativeExpressionTail -> MultiplicativeOperator Term MultiplicativeExpressionTail | .
MultiplicativeOperator -> "*" | "/" | "%" .
Term -> Factor TermTail .
TermTail -> "^" Term | .
Factor -> "!" Spec | "-" Spec | Spec .
IfStatement -> "if" Expression "then" Body ElseBlock "endif" .
ElseBlock -> "else" Body | "elseif" Expression "then" Body ElseBlock | .
LoopWithPreconditionStatements -> "while" Expression "do" Body "endwhile" | ForCycleHead "do" Body "endfor" .
ForCycleHead -> "for" ForCycleVarDeclaration "to" ForCycleTo .
ForCycleVarDeclaration -> "VARNAME" "=" Expression | "VARNAME" "=" "INT_CONST" "->" PrimitiveType | "VARNAME" "=" "CHAR_CONST" "->" PrimitiveType .
ForCycleTo -> Expression .
LoopWithPostconditionStatement -> "repeat" Body "until" Expression .

VARNAME -> "VARNAME" .
INT_CONST -> "INT_CONST" .
CHAR_CONST -> "CHAR_CONST" .
STRING_CONST -> "STRING_CONST" .
REF_CONST -> "REF_CONST" .
