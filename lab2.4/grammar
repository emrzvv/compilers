Program -> Definitions .
Definitions -> Define Definitions | .
Define -> "(" "define" DefinitionTail ")" .
DefinitionTail -> FunctionDef | VariableDef .

FunctionDef -> "(" identifier Parameters ")" ExpressionList .
VariableDef -> identifier Expression .
Parameters -> identifier ParametersTail .
ParametersTail -> Parameters | .

Expression -> "(" ExpressionExt ")" | SimpleExpr .
ExpressionExt -> 
    IfExpr | CondExpr | LetExpr | LambdaDef | BinOpExpr | ComparisonExpr .

IfExpr -> "if" Expression Expression Expression .

CondExpr -> "cond" CondBody .   
CondBody -> CondOnly CondBody | .
CondOnly -> "(" Expression Expression ")" .

LetExpr -> "let" "(" LetBindings ")" ExpressionList .

LetBindings -> LetBinding LetBindings | .
LetBinding -> "(" identifier Expression ")" .

LambdaDef -> "lambda (" Parameters ")" ExpressionList .

BinOpExpr -> BinOp ExpressionList .
ExpressionList -> Expression ExpressionListTail .
ExpressionListTail -> ExpressionList | .

ComparisonExpr -> ComparisonOp ExpressionList .
SimpleExpr -> identifier | number .

BinOp -> "+" | "-" | "*" | "/" .
ComparisonOp -> ">" | "<" | ">=" | "<=" | "=" .
